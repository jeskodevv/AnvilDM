plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'uz.jesko'
version = '1.6.8-BETA'
archivesBaseName = 'AnvilDM'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.essentialsx.net/releases/' }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'net.essentialsx:EssentialsX:2.19.7'
    compileOnly 'org.black_ixx:playerpoints:3.2.5'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

task generateVersion {
    doLast {
        file("src/main/java/uz/jesko/anvildm/Version.java").text = """
package uz.jesko.anvildm;

public class Version {
    public static final String VERSION = "$version";
}
"""
    }
}

clean {
    delete file("src/main/java/uz/jesko/anvildm/Version.java")
}

compileJava.dependsOn generateVersion

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
}

tasks.build.dependsOn tasks.shadowJar